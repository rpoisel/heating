FUNCTION I2HAE : ARRAY[0..4] OF UINT /* 5 channels with 10 bits each */

	VAR_INPUT
		fd : DINT := -1;
		address : INT := 16#0b;
	END_VAR

	VAR
		num_bytes : LINT := -1; /* number of bytes read from bus device */
		cnt : INT := 0;
		data : ARRAY[0 .. 63] OF BYTE;

		I2C_SLAVE : DINT := 1795; /* a constant from the Linux kernel */
		
		num_channels : INT := 5;
		data_len : INT := 11;
	END_VAR

	IF fd >= 0 THEN
		System_ioctl(fd := fd, request := I2C_SLAVE, data := address, ENO => ENO);
		IF ENO THEN
			num_bytes := Posix_read(fd := fd, count := data_len /* 11 bytes at once(!) */, data => data, ENO => ENO);

			IF ENO AND num_bytes = data_len THEN
				FOR cnt := 0 TO num_channels - 1 BY 1 DO
					I2HAE[cnt] := TO_UINT(data[1 + cnt * 2]) + TO_UINT(data[2 + cnt * 2]) * 256;
				END_FOR;
			END_IF;
		END_IF;
	ELSE
		ENO := FALSE;
	END_IF;

END_FUNCTION